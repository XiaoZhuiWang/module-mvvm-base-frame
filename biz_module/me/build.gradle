if(isRunModule.toBoolean()){
    apply plugin:'com.android.application'
    //使用插件applifecycle
    apply plugin: 'com.hm.plugin.lifecycle'
}else {
    apply plugin:'com.android.library'
}
apply plugin: 'com.alibaba.arouter'
apply plugin: 'kotlin-android'


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    resourcePrefix "me_"

    defaultConfig {
        if(isRunModule.toBoolean()){
            applicationId "com.dandelion.me"
        }

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }

    sourceSets {
        main {
            // 1.单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            // 我们还可以根据不同工程配置不同的 Java 源代码、不同的 resource 资源文件等的
            if (isRunModule.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation project(':common_service:common')
    implementation project(':common_service:communication')
    implementation project(':biz_base_module:open_platform')
    implementation project(':biz_base_module:push')

    annotationProcessor "com.github.hufeiyang.Android-AppLifecycleMgr:applifecycle-compiler:$rootProject.applifecycle_version"
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.arouter_version"
}